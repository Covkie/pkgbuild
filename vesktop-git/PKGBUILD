# Maintainer: zt <zt@zt64.dev>
pkgbase=vesktop-git
_pkgname=vesktop
pkgname=("vesktop-git" "vesktop-electron-git")
pkgdesc="A standalone Electron-based Discord app with improved Linux support Vencord"
pkgver=r363.24fbf35
pkgrel=1

arch=("x86_64" "aarch64")
url="https://github.com/Vencord/Vesktop"
license=('GPL-3.0-only')

depends=('alsa-lib' 'gtk3' 'nss')
makedepends=('nodejs>=18' 'git' 'electron')
optdepends=(
  'libnotify: Notifications'
  'xdg-utils: Open links, files, etc'
)

provides=("vesktop")
replaces=("vencord-desktop-git" )

source=("$_pkgname::git+$url.git" "vesktop.desktop" "vesktop.sh")

sha256sums=('SKIP'
            'c7845033716d7fdec2ea1daf8c3575b504bc6a5c19c702e3722a0e6237ae571f'
            '4a790359a465979dbf3b5d815ed0d5f3f8a381a5ae08e1b359cee40dbd81d2ad')

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}


build_common() {
  cd "$srcdir/$_pkgname"

  corepack pnpm i
  corepack pnpm package:dir
}

package_common() {
  cd "$srcdir/$_pkgname"

  # Create necessary directories
  install -d "$pkgdir/usr/lib/$_pkgname"
  install -d "$pkgdir/usr/bin"

  # Conditionally copy application files
  [[ $pkgbase == $pkgname ]] && \
    cp -R "dist/linux-unpacked/." "$pkgdir/usr/lib/$_pkgname" || \
    cp "dist/linux-unpacked/resources/app.asar" "$pkgdir/usr/lib/$_pkgname"


  install -Dm 644 "../vesktop.desktop" "$pkgdir/usr/share/applications/vesktop.desktop" # Install desktop entry
  install -Dm 644 "static/icon.png" "$pkgdir/usr/share/pixmaps/vesktop.png" # Install icon
  install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE" # Install license

  install -Dm755 "../vesktop.sh" "$pkgdir/usr/bin/$_pkgname" # Start script
}

package_vesktop-git() {
  conflicts=('vesktop-electron-git')
  build_common
  package_common
}

package_vesktop-electron-git() {
  pkgdesc="An Electron-based Discord app with improved Linux support Vencord using system provided electron"
  conflicts=('vesktop-git')
  depends=('electron')

  cd "$srcdir/$_pkgname"

  # Use system's electron
  sed -i "/linux/s/^/        \"electronDist\": \"\\/usr\\/lib\\/electron\",\n/" package.json
  sed -i "s|/usr/lib/$_pkgname/$_pkgname|electron /usr/lib/$_pkgname/app.asar|" ../vesktop.sh

  build_common
  package_common
}
